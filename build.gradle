buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven { url = "https://repo.spongepowered.org/maven" }
        maven { url = 'https://maven.parchmentmc.org' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'com.github.breadmoirai:github-release:2.4.1'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.github.breadmoirai.github-release'

version = "${mod_version}"
group = mod_group_id
archivesBaseName = mod_name

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println 'Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch')

minecraft {
    mappings channel: 'parchment', version: "2022.03.06-1.16.5"

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                gravitymod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                gravitymod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'gravitymod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                gravitymod {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url 'https://maven.minecraftforge.net' }
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/maven" }
    maven { url = 'https://maven.parchmentmc.org' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.34'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation 'org.jetbrains:annotations:24.0.0'
}

jar {
    manifest {
        attributes([
                "Specification-Title": mod_id,
                "Specification-Vendor": mod_authors,
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": archiveVersion,
                "Implementation-Vendor": mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta']
def replaceProperties = [
        mod_id     : mod_id, mod_name: mod_name, mod_version: mod_version,
        mod_authors: mod_authors
]

processResources {
    inputs.properties replaceProperties
    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

jar.finalizedBy('reobfJar')

mixin {
    add sourceSets.main, "mixins.gravitymod.refmap.json"
    config "mixins.gravitymod.json"
}

githubRelease {
    token System.getenv("GITHUB_TOKEN")
    owner "Weever1337"
    repo "GravityProject"
    tagName "v${project.version}"
    releaseName "Latest 1.16.5 v${project.version}"
    targetCommitish "1.16.5-forge"
    generateReleaseNotes true
    releaseAssets = [jar.archivePath]
    draft false
    prerelease false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'gravityproject'
            artifact jar
        }
    }
    repositories {
        maven {
            url 'https://pkgs.dev.azure.com/weever1337/60e8f2cf-4d94-41e0-9b30-f900d6f1459d/_packaging/gravityproject/maven/v1'
            name 'gravityproject'
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}
